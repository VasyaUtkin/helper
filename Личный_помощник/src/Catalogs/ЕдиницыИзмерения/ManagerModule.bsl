#Область ПрограммныйИнтерфейс

Процедура ЗаполнитьДаннымиИзМакета()Экспорт
	
	Макет = Справочники.ЕдиницыИзмерения.ПолучитьМакет("КлассификаторОКЕИ_КоммунальныеУслуги");
	КолонкиМакета = СтруктураСоответствияКолонокМакетаРеквизитам();
	НомерСтроки = 2;
	Пока Истина Цикл
		
		Код = Макет.Область("R" + НомерСтроки + "C1").Текст;
		Если Не ЗначениеЗаполнено(Код) Тогда
			Прервать;	
		КонецЕсли;
		
		ЭлементСправочника = Справочники.ЕдиницыИзмерения.НайтиПоКоду(Код, Истина);
		Если Не ЗначениеЗаполнено(ЭлементСправочника) Тогда
			ОбъектСправочника = Справочники.ЕдиницыИзмерения.СоздатьЭлемент();
			ОбъектСправочника.Код = Код;
		Иначе
			ОбъектСправочника = ЭлементСправочника.ПолучитьОбъект();
		КонецЕсли;
		
		Для Каждого КолонкаМакета Из КолонкиМакета Цикл
			ОбъектСправочника[КолонкаМакета.Ключ] = Макет.Область("R" + НомерСтроки + КолонкаМакета.Значение).Текст;	
		КонецЦикла;
		
		ОбъектСправочника.Записать();
		
		НомерСтроки = НомерСтроки + 1;
			
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеМетоды

Функция СтруктураСоответствияКолонокМакетаРеквизитам()
	
	Результат = Новый Структура;
	Результат.Вставить("Наименование", "C2");
	Результат.Вставить("УсловноеОбозначениеНациональное", "C3");
	Результат.Вставить("УсловноеОбозначениеМеждународное", "C4");
	Результат.Вставить("КодовоеСимвольноеОбозначениеНациональное", "C5");
	Результат.Вставить("КодовоеСимвольноеОбозначениеМеждународное", "C6");
	Возврат Результат;
	
КонецФункции


#КонецОбласти