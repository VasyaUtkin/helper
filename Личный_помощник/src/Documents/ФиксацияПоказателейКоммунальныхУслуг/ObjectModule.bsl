
//////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	ЭтотОбъект.Дата = ТекущаяДата();
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, Режим)

	Для Каждого ТекСтрокаУслуги Из Услуги Цикл
		
		Если ТекСтрокаУслуги.Сумма <> 0 Тогда
			Движение = Движения.ФиксацияПоказателейКоммунальныхУслуг.Добавить();
			Движение.Период = Дата;
			Движение.ВидУслуг = ТекСтрокаУслуги.Услуга;
			Движение.Количество = ТекСтрокаУслуги.ПотребляемоеЗначение;
			Движение.Сумма = ТекСтрокаУслуги.Сумма;			
		КонецЕсли;
		
		Если ТекСтрокаУслуги.ТекущееПоказаниеСчетчика <> 0 Тогда
			Движение = Движения.ПоказанияСчетчиков.Добавить();
			Движение.Период = Дата;
			Движение.ВидУслуги = ТекСтрокаУслуги.Услуга;
			Движение.Значение = ТекСтрокаУслуги.ТекущееПоказаниеСчетчика;	
		КонецЕсли;
	
	КонецЦикла;
	
	Движения.ФиксацияПоказателейКоммунальныхУслуг.Записывать = Истина;
	Движения.ПоказанияСчетчиков.Записывать = Истина;
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если РежимЗаписи = РежимЗаписиДокумента.Проведение Или РежимЗаписи = РежимЗаписиДокумента.ОтменаПроведения Тогда
		
		// Очистим движения
		Если Не ЭтотОбъект.ЭтоНовый() Тогда
			// Надо очистить все регистры.
			Движения.ФиксацияПоказателейКоммунальныхУслуг.Записывать = Истина;
			#Если МобильноеПриложениеСервер Тогда
				// Обход ошибки моб. платформы 10143497
				Движения.ФиксацияПоказателейКоммунальныхУслуг.Очистить();
				// Конец  Обход ошибки моб. платформы 10143497
			#КонецЕсли 
			Движения.Записать();
			
		КонецЕсли;
		
	КонецЕсли;

КонецПроцедуры
